10 REM MANDELBROT FOR PIGFX V0.1
20 GOSUB 9000
30 FOR I=-N1 TO N1 STEP SP
40 X0=((N1-I)*X1+(N1+I)*X2)/(2*N1)
50 FOR J=0 TO N2 STEP SP
60 Y0=((N2-J)*Y1+(N2+J)*Y2)/(2*N2)
70 X=X0:Y=Y0:K=0
80 Z=X:X=X*X-Y*Y+X0
90 Y=2*Y*Z+Y0
100 IF X*X+Y*Y>4 THEN 120
110 K=K+1:IF K<NI THEN 80
120 CC=K+1:IF CC<1 OR CC>LK THEN 140
130 PRINT CHR$(27);"[38;5;";STR$(PA(CC));"m";:REM COLOR
132 PRINT CHR$(27);"[#";STR$(CX+I);";";STR$(CY-J);";2;2r";:REM PLOT
134 PRINT CHR$(27);"[#";STR$(CX+I);";";STR$(CY+J);";2;2r";:REM PLOT
136 PRINT CHR$(27);"[H";:REM CURSOR HOME TO PREVENT SCROLL
140 NEXT J
141 *OPT 0
142 PRINT TAB(0,1);(N1-I);"  ";
143 *OPT 1
150 NEXT I
160 PRINT CHR$(7);
170 *OPT 0
180 PRINT:PRINT "TIME: ";STR$(TIME-TI);" SECONDS"
190 END
9000 RESTORE 9210
9010 DIM PA(20)
9020 FOR I=1 TO 20
9030 READ PA(I)
9040 NEXT I
9050 SC=1:MX=640:MY=480:LK=20
9060 CX=MX/2:CY=MY/2
9070 X1=-2.05:X2=0.7
9080 Y1=-1.18:Y2=1.18
9090 S1=640
9100 SP=2
9110 NI=LK
9115 CLS:PRINT "MANDELBROT COUNTDOWN:"
9120 *OPT 1:REM OUTPUT TO SERIAL PORT 2
9125 CLS
9126 PRINT CHR$(27);"[?25l";:REM CURSOR INVISIBLE
9130 N1=S1/2:N2=N1*(MY/MX):ZK=NI+1
9135 TI=TIME
9140 RETURN
9210 DATA 16,17,18,19,20,21,22,23,24,25,26,27,28,29,30,31,32,33,34,35
